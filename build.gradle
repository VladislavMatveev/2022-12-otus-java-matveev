plugins {
    id "io.spring.dependency-management" version "1.1.0"
    id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
    id "org.springframework.boot" version "3.0.1" apply false
    id "com.google.cloud.tools.jib" version "3.2.1" apply false
    id "fr.brouillard.oss.gradle.jgitver" version "0.10.0-rc03" apply false
}

allprojects {
    group "ru.otus"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom "org.springframework.boot:spring-boot-dependencies:3.0.1"
                mavenBom "org.testcontainers:testcontainers-bom:1.17.6"
                mavenBom "com.google.protobuf:protobuf-bom:3.21.12"
            }
            dependency "com.google.guava:guava:${Versions.guava}"
            dependency("org.glassfish:jakarta.json:${Versions.glassfishJson}")

            dependency("org.projectlombok:lombok:${Versions.lombok}")
            dependency("com.google.code.gson:gson:${Versions.gson}")
            dependency("com.datastax.oss:java-driver-core:${Versions.cassandra}")

        }
    }

    configurations.configureEach {
        resolutionStrategy {
            failOnVersionConflict()
        }
        resolutionStrategy {
            force("javax.servlet:servlet-api:2.4")
            force("commons-logging:commons-logging:1.1.1")
            force("commons-lang:commons-lang:2.5")
            force("org.codehaus.jackson:jackson-core-asl:1.8.8")
            force("org.codehaus.jackson:jackson-mapper-asl:1.8.3")
            force("org.codehaus.jettison:jettison:1.1")
            force("net.java.dev.jna:jna:5.8.0")
            force("com.google.errorprone:error_prone_annotations:2.7.1")
        }
    }
}